/*
 * Gradle file to build the Google Play Instant Plugin for Unity.
 * See https://docs.unity3d.com/Manual/CommandLineArguments.html
 */
buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
}

/*
 * Project level variables.
 */
project.ext {
    // Search for the Unity Editor executable.
    unityExe = System.getProperty('UNITY_EXE')
    if (unityExe == null || unityExe.isEmpty()) {
        unityExe = System.getenv('UNITY_EXE')
    }
    if (unityExe == null || unityExe.isEmpty()) {
        def osName = System.getProperty('os.name').toLowerCase();
        if (osName.contains('mac os x')) {
            unityExe = '/Applications/Unity/Unity.app/Contents/MacOS/Unity'
        } else if (osName.contains('windows')) {
            unityExe = 'C:\\Program Files\\Unity\\Editor\\Unity.exe'
        } else if (os_name.contains('linux')) {
            unityExe = '/opt/Unity/Editor/Unity'
        } else {
            throw new GradleException("Unsupported OS: $osName")
        }
    }
    def unityExeFound = (new File(unityExe)).exists();
    if (!unityExeFound) {
        throw new GradleException('Unity Editor executable not found')
    }

    // If the git project is located directly under the Assets folder, then we move up two
    // directories for the root of the build path. This avoids conflicts in a working Unity
    // project where we'd copy files and get duplicate class name build errors.
    def rootPath = file('.')
    def assetsPath = rootPath.getParentFile()
    if (assetsPath.getName() == 'Assets') {
        rootPath = assetsPath.getParentFile()
    }
    playInstantBuildPath = "${rootPath}/play-instant-build"

    pluginVersion = '0.9'
}

def copyFiles(projectPath, exclusions) {
    description 'Copies files into specified build staging location except for noted exclusions'
    copy {
        from 'GooglePlayInstant', 'LICENSE', 'README.md', 'CONTRIBUTING.md'
        into "${projectPath}/Assets/GooglePlayInstant"
        exclude 'Samples', '**/*.meta'
        exclude exclusions
    }
}

def exportUnityPackage(projectPath, packageFilePrefix) {
    description 'Creates a .unitypackage file with the specified prefix'
    def packageFileName = "${packageFilePrefix}-${pluginVersion}.unitypackage"
    def exportFileName = file(packageFileName).getPath()
    def logFile = file("${packageFileName}.log").getPath()
    def argv = [
            '-batchmode',
            '-projectPath',
            projectPath,
            '-logFile',
            logFile,
            '-exportPackage',
            'Assets',
            exportFileName,
            '-quit'
    ]
    exec {
        executable unityExe
        args argv
    }
}

task exportPlugin {
    description 'Creates and exports the Plugin as a .unitypackage'
    def buildPath = "${playInstantBuildPath}/plugin"
    doLast {
        copyFiles(buildPath, ['Tests'])
        exportUnityPackage(buildPath, 'google-play-instant-plugin')
    }
}

task exportSampleSphereBlast {
    description 'Creates and exports the Plugin unity package'
    def buildPath = "${playInstantBuildPath}/sphereblast"
    doFirst {
        copyFiles(buildPath, ['Tests'])
        copy {
            from 'GooglePlayInstant/Samples/SphereBlast'
            into "${buildPath}/Assets/SphereBlast"
        }
    }
    doLast {
        exportUnityPackage(buildPath, 'google-play-instant-sphereblast')
    }
}

task buildAndRunInstantAppTest {
    description 'Creates the instant app test project, then builds and runs in instant mode'
    def buildAndRunFunction = 'GooglePlayInstant.Editor.AndroidInstantTest.BuildAndRunTestProject'
    def buildPath = "${playInstantBuildPath}/testproject"
    def logFile = file("testproject.log").getPath()
    doFirst {
        copyFiles(buildPath, [])
        copy {
            from 'GooglePlayInstant/Samples/TestProject'
            into "${buildPath}/Assets/TestProject"
        }
    }
    doLast {
        def argv = [
            '-batchmode',
            '-projectPath',
            buildPath,
            '-logFile',
            logFile,
            '-buildTarget',
            'Android',
            '-executeMethod',
            buildAndRunFunction,
            'quit'
        ]
        exec {
            executable unityExe
            args argv
        }
    }
}

task tests {
    description 'Runs unit tests with Unity test runner'
    def buildPath = "${playInstantBuildPath}/tests"
    doFirst {
        copyFiles(buildPath, [])
    }
    doLast {
        def argv = [
                '-batchmode',
                '-projectPath',
                buildPath,
                '-logFile',
                'test.log',
                '-runEditorTests'
        ]
        exec {
            executable unityExe
            args argv
        }
    }
}

task clean(type: Delete) {
    description 'Removes build artifacts'
    delete playInstantBuildPath
    delete fileTree('.') {
        include 'local.properties', 'local.properties.meta',
                '*.log', '*.log.meta',
                '*.unitypackage', '*.unitypackage.meta',
                'TestResults*.xml', 'TestResults*.xml.meta'
    }
}

project.defaultTasks = ['tests', 'exportPlugin']
